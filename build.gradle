// Gradle plugins
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/releases'
        }
    }

    dependencies {
        classpath 'net.minecrell:VanillaGradle:2.0.3_1'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-20160116.192356-17'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.5.1'
    id 'nebula.provided-base' version '3.0.3'
    id 'net.minecrell.licenser' version '0.1.5'
}

apply plugin: 'net.minecrell.vanilla.server.library'

group = 'net.minecrell'
description = 'Simple Sponge plugin extending the status ping API with internal protocol versions'
version = '0.2-SNAPSHOT'

minecraft {
    version = '1.8.9'
    mappings = 'stable_22'
}

dependencies {
    provided 'org.spongepowered:spongeapi:4.0.3'
}

processResources {
    from 'LICENSE'
}

jar {
    exclude 'org/spongepowered/**'
}

reobf.jar {
    mappingType = 'SEARGE'
}

sourceJar {
    exclude 'org/spongepowered/**'
}

javadoc {
    exclude 'org/spongepowered/**'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file://localhost' +
                    (System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir.absolutePath/repo"))
        }
    }
}

tasks.withType(Upload) {
    dependsOn 'reobfJar'
}

license {
    header rootProject.file('LICENSE')
    include '**/*.java'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
